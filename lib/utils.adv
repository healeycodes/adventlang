let get_puzzle_num = func(path) {
    let puzzle = [];
    read_lines(path, func(s) {
        puzzle.append(num(s));
    });
    return puzzle
};

let get_puzzle_str = func(path) {
    let puzzle = [];
    read_lines(path, func(s) {
        puzzle.append(s);
    });
    return puzzle
};

let copy_list = func(l) {
    let ret = [];
    for (let i = 0; i < len(l); i = i + 1) {
        ret.append(l[i]);
    }
    return ret;
};
(func() {
    let l1 = [0];
    let l2 = copy_list(l1);
    l2.pop();
    assert(len(l2), 0);
    assert(len(l1), 1);
})();

let map = func(l, f) {
    let ret = [];
    for (let i = 0; i < len(l); i = i + 1) {
        ret.append(
            f(l[i])
        );
    }
    return ret;
};
(func() {
    let l1 = [0, 2, 4];
    let squared = map(l1, func(n) { return n * n });
    assert(len(squared), 3);
    assert(squared[0], 0);
    assert(squared[1], 4);
    assert(squared[2], 16);
})();

let concat = func(l1, l2) {
    for (let i = 0; i < len(l2); i = i + 1) {
        l1.append(l2[i]);
    }
    return l1
};
(func() {
    let l1 = [0];
    let l2 = [1];
    concat(l1, l2);
    assert(len(l1), 2);
    assert(l1[0], 0);
    assert(l1[1], 1);
    // Shouldn't error
    concat(l1, []);
})();

let slice = func(l, from, to) {
    let ret = [];
    for (let i = from; i < to; i = i + 1) {
        ret.append(l[i]);
    }
    return ret
};
(func() {
    let l1 = [0, 1, 2, 3];
    let sliced = slice(l1, 1, 3);
    assert(len(sliced), 2);
    assert(sliced[0], 1);
    assert(sliced[1], 2);
})();

let foreach = func(l, f) {
    for (let i = 0; i < len(l); i = i + 1) {
        f(l[i]);
    }
};
(func() {
    let sum = 0;
    let l1 = [0, 1, 2, 3];
    foreach(l1, func(item) { sum = sum + item; });
    assert(sum, 6);
})();

let sort = func (l) {
    if (len(l) <= 1) {
        return l;
    }
    let pivot = l[0];
    let left = []; 
    let right = [];
    for (let i = 1; i < len(l); i = i + 1) {
        if (l[i] < pivot) {
            left.append(l[i]);
        } else {
            right.append(l[i]);
        }
    }
    let ret = sort(left);
    ret.append(pivot);
    let rest = sort(right);
    for (let i = 0; i < len(rest); i = i + 1) {
        ret.append(rest[i]);
    }
    return ret;
};
assert(sort([2, 1, 0])[0], 0);
assert(sort([3, 1, -1])[2], 3);
